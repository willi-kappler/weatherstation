'CR1000 Series Datalogger

'This is a sample program that sends data from the COM9602 Satelite communcation module
'A flag exists that transmits data table definition information once after a new program 
'start
  
'Data for reference/Test
Public PTemp, BattV,

Units PTemp = Degree C
Units BattV = V

'serial port for communications
Const SBDComPort = COM1

'Define how many data tables are to be sent using SBD communications
Const NumberDataTables=2

'Require a variable for when a new DataTable file for transmission is created
Public NewTableFile(NumberDataTables) As Boolean
Public SendSbdMessage(NumberDataTables) As Boolean

'Require strings for the unique file names associated with the data table files
Public TableFileName(NumberDataTables) As String * 40

'used to send complete data definition information
'Normally sent once after the program starts
Public DataDefinitionFlag(NumberDataTables) As Boolean

'A flag that will perform an initialization on the Modem to disable handshaking
Public InitializeModem As Boolean

Public SBD_TestTransmissionFlag As Boolean

'This public text from the COM9602 subroutine can be viewed or stored for diagnostics.
Public COM9602_ResultString As String *50

Dim DataTableCount As Long
Dim cntr2 As Long

'Hourly Data Table
'This is Table "1"
DataTable (TableOne,1,-1)
  DataInterval (0,60,Min,0)
  TableFile ("USR:TableOne",1,0,1,0,0,NewTableFile(1),TableFileName(1))
  Minimum (1,BattV,FP2,0,False)
  Sample (1,PTemp,FP2)
EndTable

'Daily Data Table
'This is Table "2"
DataTable (TableTwo,1,-1)
  DataInterval (0,1440,Min,0)
  TableFile ("USR:TableTwo",1,0,1,0,0,NewTableFile(2),TableFileName(2))
  Minimum (1,BattV,FP2,0,False)
  Sample (1,PTemp,FP2)
EndTable

Include "CPU:Com9602-CR1000_Sub_str_Enc.CR1"

BeginProg
  'A USR Drive is required for the COM9602 subroutine to store some temporary files.
  SetStatus ("USRDriveSize",32768)

  'Setup communication port for the COM9602
  SerialOpen (SBDComPort,19200,3,0,400)

  DataDefinitionFlag(1) = True
  DataDefinitionFlag(2) = True

  'Perform modem initialization
  InitializeModem = True

  'Configure the power control port for the COM9602 as an output
  'Set C3 as output
  PortsConfig (&B100,&B100)

  Scan (5,Sec,0,0)
    PanelTemp (PTemp,250)
    Battery (BattV)

    For DataTableCount=1 To NumberDataTables Step 1
      If NewTableFile(DataTableCount)=True Then
        SendSbdMessage(DataTableCount)=True
        NewTableFile(DataTableCount)=False
      EndIf
    Next DataTableCount

    'Must call the data tables
    CallTable TableOne
    CallTable TableTwo

  NextScan

  SlowSequence

  Scan(60,sec,0,0)

    If SBD_TestTransmissionFlag = True Then

      'Set Flag false so that only one test transmission is sent
      SBD_TestTransmissionFlag = False

      'Turn on power
      WriteIO (&B100,&B100)
      'wait for warmup
      Delay(1,2,sec)

      'Call the test subroutine
      Call SBD_TestTransmission(SBDComPort,COM9602_ResultString)

      Delay(1,1,sec)
      WriteIO (&B100,&B000)
      'Must delay 1 second for modem to power off fully
      Delay(1,2,sec)
    EndIf

    'The following Checks if there is a new data file created from all required outputs
    'If a new data output exists, call a subrouting to prepare the data and tansmit it.
    For cntr2=1 To NumberDataTables Step 1
      If SendSbdMessage(cntr2) Then
        'Power on the Modem according to the channel using C3
        WriteIO (&B100,&B100)
        'wait for warmup
        Delay(1,2,sec)

        Call SendSBD(SBDComPort,TableFileName(cntr2), cntr2, InitializeModem, DataDefinitionFlag(cntr2), COM9602_ResultString)
        'Power off the Modem
        'Using C3
        'wait for shutdown
        Delay(1,1,sec)
        WriteIO (&B100,&B000)
        'Must delay 1 second for modem to power off fully
        Delay(1,2,sec)
        SendSbdMessage(cntr2) = False
      EndIf
    Next cntr2

  NextScan
EndProg


